%%%
  VERSION:1
  LANGUAGE:ENGLISH
%%%

MODULE BE_User(SYSMODULE)
    !
    ! 2007-07-01 S?
    !   CHG: Added data for tcp check freq. from BE_Mnu.
    ! 2006-01-17 S?
    !   CHG: Added data scanMig and tdMig for use in setup.
    !   CHG: Renamed procedures due to name conflicts with bullseye.
    ! 2006-01-13 S?
    !   CHG: Changed name to BE_User due to restore problem with BullsEye, name conflict
    !
    TASK PERS tooldata tWeldGun:=[TRUE,[[0,0,0],[1,0,0,0]],[1,[1,0,0],[1,0,0,0],0,0,0]];
    !
    LOCAL CONST jointtarget jtEmpty:=[[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    TASK PERS jointtarget jtBEApprPos:=[[-156.571,-17.364,15.6462,16.512,50.3656,-22.0886],[4878.69,9E+09,9E+09,9E+09,9E+09,9E+09]];
    TASK PERS jointtarget jtBEStartPos:=[[-152.137,-15.5781,26.3759,8.87603,41.7339,-17.7744],[4878.69,9E+09,9E+09,9E+09,9E+09,9E+09]];
    !
    !CONST be_device devUp:=["sen2",TRUE,[6,45,100,100],[40,45,100,100],0.10,FALSE,TRUE,TRUE];
    CONST be_device devUp:=["sen2",TRUE,[6,45,100,100],[40,45,100,100],0.10,FALSE,TRUE,TRUE];

    CONST be_device devDown:=["sen2",TRUE,[6,45,100,100],[40,45,100,100],0.10,TRUE,TRUE,TRUE];
    CONST be_scan scanMig:=[2,8,8,45,35,25];
    !CONST be_tooldesign tdMig:=[TRUE,30,18,50,6,6,TRUE,TRUE,1.2,[150,120,100,100],[250,150,100,100]];
    CONST be_tooldesign tdMig:=[TRUE,30,18,40,6,6,TRUE,TRUE,1.2,[150,120,100,100],[250,150,100,100]];

    !
    TASK PERS num nTcpExtension:=20;
    ! vars for tcp check during production
    TASK PERS num nBECheckTcpFreq:=0;
    TASK PERS num nBECheckCycle:=0;
    TASK PERS num numElapsedTime:=664.793;
    TASK PERS num numMeanDev:=0.170572;
    TASK PERS num numMaxDev:=0.243017;

    !
    PROC BEToolSetup()
        VAR jointtarget jCurrPos;
        VAR string sHeader;
        VAR string sMsg{5};
        VAR btnres nAnswer;

        sHeader:="BullsEye"+" ("+GetTaskName()+")";
        sMsg{1}:="Press Yes to run setup for BullsEye scanning process.";
        sMsg{2}:="";
        sMsg{3}:="Press No to cancel.";
        sMsg{4}:="";
        sMsg{5}:="";
        UIMsgBox\Header:=sHeader,sMsg{1}\MsgLine2:=sMsg{2}\MsgLine3:=sMsg{3}\MsgLine4:=sMsg{4}\MsgLine5:=sMsg{5}\Buttons:=btnYesNo\Icon:=iconInfo\Result:=nAnswer;
        MoveAbsJ [[-90.2484,-42.8506,26.294,10.1506,63.675,-12.0225],[4878.69,9E+09,9E+09,9E+09,9E+09,9E+09]]\NoEOffs, v1000, z50, toolWeldGun2;
        IF (nAnswer=resYes) THEN
            !The length of the TCP extension, as measured from the end of the tool body, is defined
            !here in millimeters.
            sHeader:="BullsEye"+" ("+GetTaskName()+")";
            sMsg{1}:="TCP extension";
            sMsg{2}:="The length of the TCP extension, measured from the end of the tool body in mm.";
            sMsg{3}:="";
            sMsg{4}:="Current value: "+ValToStr(nTcpExtension);
            sMsg{5}:="Enter TCP extension (mm)";
            nTcpExtension:=UINumEntry(\Header:=sHeader\MsgArray:=sMsg\Icon:=iconInfo\InitValue:=nTcpExtension);

            sHeader:="BullsEye"+" ("+GetTaskName()+")";
            sMsg{1}:="Tool and positions";
            sMsg{2}:="";
            sMsg{3}:="Press Ok to modpos approach position, start position";
            sMsg{4}:="and select tool for BullsEye scanning process.";
            sMsg{5}:="";
            UIMsgBox\Header:=sHeader,sMsg{1}\MsgLine2:=sMsg{2}\MsgLine3:=sMsg{3}\MsgLine4:=sMsg{4}\MsgLine5:=sMsg{5}\Buttons:=btnOk\Icon:=iconInfo\Result:=nAnswer;

            Stop\NoRegain;
            ! modpos robtargets jtBEApprPos and jtBEStartPos
            ClkReset clockPartTimeConsume;
            ClkStart clockPartTimeConsume;
            BESetupToolJ jtBEApprPos,jtBEStartPos,nTcpExtension,tdMig,scanMig,devUp,v100,fine,toolWeldGun2\ElapsedTime:=numElapsedTime\MeanDev:=numMeanDev\MaxDev:=numMaxDev;
            Logging\INFO, "ElapsedTime="+ValToStr(numElapsedTime)+", MeanDev="+ValToStr(numMeanDev)+", MaxDev="+ValToStr(numMaxDev);
            toolWeldGun:=toolWeldGun2;
            toolLaser2.tload:=toolWeldGun2.tload;
            numTimeConsumeTotal:=ClkRead(clockPartTimeConsume);
            Logging\INFO, "BESetupToolJ: numTimeConsumeTotal="+ValToStr(numTimeConsumeTotal);
            ClkStop clockPartTimeConsume;
            MoveAbsJ [[-90.2484,-42.8506,26.294,10.1506,63.675,-12.0225],[4878.69,9E+09,9E+09,9E+09,9E+09,9E+09]]\NoEOffs, v1000, z50, toolWeldGun2;
        ENDIF
    ENDPROC

    PROC BEToolCheck()
        ClkReset clockPartTimeConsume;
        ClkStart clockPartTimeConsume;
        BECheckTcp toolWeldGun2\XYZOnly\SingleScan;
        toolWeldGun:=toolWeldGun2;
        numTimeConsumeTotal:=ClkRead(clockPartTimeConsume);
        Logging\INFO, "BEToolCheck: numTimeConsumeTotal="+ValToStr(numTimeConsumeTotal);
        ClkStop clockPartTimeConsume;
    ENDPROC

    PROC BEToolUpdate()
        ClkReset clockPartTimeConsume;
        ClkStart clockPartTimeConsume;
        BEUpdateTcp toolWeldGun2\XYZOnly;
        toolWeldGun:=toolWeldGun2;
        numTimeConsumeTotal:=ClkRead(clockPartTimeConsume);
        Logging\INFO, "BEToolUpdate: numTimeConsumeTotal="+ValToStr(numTimeConsumeTotal);
        ClkStop clockPartTimeConsume;
    ENDPROC

    PROC BERefPnt()
        BERefPointer toolWeldGun2;
        toolWeldGun:=toolWeldGun2;
    ENDPROC

    PROC BEExtend()
        BETcpExtend toolWeldGun2\Absolute:=nTcpExtension;
        toolWeldGun:=toolWeldGun2;
    ENDPROC
ENDMODULE
